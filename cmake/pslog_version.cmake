set(PSLOG_VERSION_OVERRIDE "" CACHE STRING
    "Override the resolved libpslog release version (for example 0.1.0 or 0.1.0-rc.1)")

set(_pslog_default_version "0.0.0")

if(DEFINED PSLOG_VERSION_SOURCE_DIR AND NOT PSLOG_VERSION_SOURCE_DIR STREQUAL "")
    set(_pslog_version_source_dir "${PSLOG_VERSION_SOURCE_DIR}")
else()
    get_filename_component(_pslog_version_source_dir "${CMAKE_CURRENT_LIST_DIR}/.." ABSOLUTE)
endif()

set(_pslog_resolved_version "${_pslog_default_version}")

if(NOT PSLOG_VERSION_OVERRIDE STREQUAL "")
    set(_pslog_resolved_version "${PSLOG_VERSION_OVERRIDE}")
else()
    find_program(PSLOG_GIT_EXECUTABLE NAMES git)
    if(PSLOG_GIT_EXECUTABLE)
        execute_process(
            COMMAND "${PSLOG_GIT_EXECUTABLE}" -C "${_pslog_version_source_dir}" describe --tags --exact-match HEAD
            RESULT_VARIABLE _pslog_git_describe_result
            OUTPUT_VARIABLE _pslog_git_describe_output
            ERROR_QUIET
            OUTPUT_STRIP_TRAILING_WHITESPACE
        )
        if(_pslog_git_describe_result EQUAL 0
           AND _pslog_git_describe_output MATCHES "^v([0-9]+\\.[0-9]+\\.[0-9]+(-[0-9A-Za-z.-]+)?(\\+[0-9A-Za-z.-]+)?)$")
            set(_pslog_resolved_version "${CMAKE_MATCH_1}")
        endif()
    endif()
endif()

if(NOT _pslog_resolved_version MATCHES "^([0-9]+)\\.([0-9]+)\\.([0-9]+)(-[0-9A-Za-z.-]+)?(\\+[0-9A-Za-z.-]+)?$")
    message(FATAL_ERROR
        "invalid libpslog version '${_pslog_resolved_version}'; expected X.Y.Z with optional prerelease/build metadata")
endif()

set(PSLOG_RESOLVED_VERSION "${_pslog_resolved_version}")
set(PSLOG_VERSION_MAJOR "${CMAKE_MATCH_1}")
set(PSLOG_VERSION_MINOR "${CMAKE_MATCH_2}")
set(PSLOG_VERSION_PATCH "${CMAKE_MATCH_3}")
set(PSLOG_PROJECT_VERSION_CORE
    "${PSLOG_VERSION_MAJOR}.${PSLOG_VERSION_MINOR}.${PSLOG_VERSION_PATCH}")

message(STATUS "libpslog release version: ${PSLOG_RESOLVED_VERSION}")
